dnl -*- m4-mode -*-
dnl  
dnl  Copyright (C) 2001 Andrew T. Csillag <drew_csillag@geocities.com>
dnl  
dnl	 This program is free software; you can redistribute it and/or modify
dnl	 it under the terms of the GNU General Public License as published by
dnl	 the Free Software Foundation; either version 2 of the License, or
dnl	 (at your option) any later version.
dnl  
dnl	 This program is distributed in the hope that it will be useful,
dnl	 but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl	 MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl	 GNU General Public License for more details.
dnl  
dnl	 You should have received a copy of the GNU General Public License
dnl	 along with this program; if not, write to the Free Software
dnl	 Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111, USA.
dnl   
dnl Process this file with autoconf to produce a configure script.
AC_INIT(SkunkWeb/mod_skunkweb/mod_skunkweb.c)

dnl add add help macro
AC_DEFUN(AC_ADD_HELP,
[AC_DIVERT_PUSH(AC_DIVERSION_NOTICE)dnl
ac_help="$ac_help
[$1]"
AC_DIVERT_POP()])

dnl Set some defaults
AC_PREFIX_DEFAULT(/usr/local/skunk)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PROG_INSTALL

dnl # Make sure xargs is GNU xargs
AC_PATH_PROG(XARGS,xargs)

if test -z $XARGS
then
    AC_MSG_ERROR([xargs program not found!])
fi

AC_SUBST(DOCROOT)
AC_ARG_WITH(docroot,
[ --docroot     Specify default docroot location (default <SkunkRoot>/docroot)], [
DOCROOT=$withval], [
DOCROOT=])

AC_SUBST(CACHE)
AC_ARG_WITH(cache,
[ --cache     Specify default cache location (default <SkunkRoot>/cache)], [
CACHE=$withval], [
CACHE=])

AC_SUBST(DOCSDIR)
AC_ARG_WITH(docsdir,
[ --docsdir    Specify location for documentation (default <SkunkRoot>/docs)], [
DOCSDIR=$withval], [
DOCSDIR=])

AC_SUBST(WITH_SKUNKWEB)
AC_ARG_WITH(skunkweb, 
[  --with-skunkweb       Install SkunkWeb as part of this installation (default yes)], [
	case $withval in
	no) WITH_SKUNKWEB=;;
	yes) WITH_SKUNKWEB=SkunkWeb;;
	*) WITH_SKUNKWEB=SkunkWeb;;
	esac], [
	WITH_SKUNKWEB=SkunkWeb])

# look for python
AC_MSG_CHECKING([--with-python given])
AC_ARG_WITH(python,
[  --with-python=/path/python   Specify path to python executable],[
	AC_MSG_RESULT([yes $withval])
	PYTHON=$withval],[
	AC_MSG_RESULT([no, will look in \$PATH])
	PYTHON=])

if test -z "$PYTHON"
then
# Check for python in path
AC_PATH_PROG(PYTHON,python)
fi

if test -z $PYTHON
then
    AC_MSG_ERROR([python interpreter is not found!])
fi

# Check python version
AC_MSG_CHECKING([python version])
if $PYTHON <<\EOF 
import sys
import string

vers = string.split ( sys.version, ' ' )[[0]]

sys.stdout.write ( 'version %s ' % vers )

sys.exit ( not (vers >= '2.1') )
EOF
then
    AC_MSG_RESULT([OK])
else
    AC_MSG_ERROR([python interpreter should be version 2.1 or newer])
fi

AC_MSG_CHECKING([Python development environment])
if $PYTHON <<\EOF
import sys
import string
mf = sys.prefix + '/lib/python%s/config/Makefile' % sys.version[[:3]]
try:
    open(mf, 'r')
except:
    sys.exit(1)
else:
    sys.exit(0)
EOF
then
    AC_MSG_RESULT([OK])
else
    AC_MSG_ERROR([Python development files missing!  
a) did you install Python via RPM?
b) did you install the Python-dev RPM
])
fi
# Check crypt module
AC_MSG_CHECKING([python crypt module])
if $PYTHON <<\EOF 
import sys
try:
    import crypt
except ImportError:
    sys.exit ( 1 )
sys.exit(0)
EOF
then
    AC_MSG_RESULT([OK])
else
    AC_MSG_RESULT([WARNING: python is not compiled with crypt module support!
The BasicAuth module will have reduced functionality, but this is probably
not a problem.
])
fi

# check for mx package
AC_MSG_CHECKING([eGenix mxBase Python package])
if $PYTHON <<\EOF 
import sys
try:
    import mx.DateTime
except ImportError:
    sys.exit ( 1 )
sys.exit(0)
EOF
then
    AC_MSG_RESULT([OK])
else
    AC_MSG_RESULT([ERROR: Skunk requires the eGenix mxBase Python package
Get it at:
    http://www.lemburg.com/files/python/eGenix-mx-Extensions.html
])
fi

dnl # Check for location of bash
dnl AC_SUBST(BASH)
dnl AC_PATH_PROG(BASH,bash)
dnl 
dnl if test -z $BASH
dnl then
dnl 	AC_MSG_ERROR([bash (Bourne Again Shell) is not found!])
dnl fi

AC_SUBST(OWNER)
AC_ARG_WITH(user, 
[  --with-user=user        Make Skunk installation owned by user], [
	OWNER=$withval], [
	OWNER=`python -c "import pwd,os; print pwd.getpwuid(os.getuid())[[0]]"`;])

AC_SUBST(GROUP)
AC_ARG_WITH(group, 
[  --with-group=group      Make Skunk installation owned by group], [
	GROUP=$withval], [
	GROUP=`python -c "import grp,os; print grp.getgrgid(os.getgid())[[0]]"`;])

dnl Checks for library functions.

# This is for mxDateTime module
AC_SUBST(MX_DATE_FUNCS)

AC_CHECK_FUNC(strftime,[MX_DATE_FUNCS="$MX_DATE_FUNCS -DHAVE_STRFTIME"])
AC_CHECK_FUNC(strptime,[MX_DATE_FUNCS="$MX_DATE_FUNCS -DHAVE_STRPTIME"])
AC_CHECK_FUNC(timegm,[MX_DATE_FUNCS="$MX_DATE_FUNCS -DHAVE_TIMEGM"])

AC_PATH_PROG(LYNX, lynx)
AC_PATH_PROG(LATEX, latex)
AC_PATH_PROG(PDFLATEX, pdflatex)
AC_PATH_PROG(DVIPS, dvips)

AC_CONFIG_SUBDIRS(SkunkWeb)

AC_ADD_HELP([
Additional options if building SkunkWeb:
  --with-docroot          Specify default docroot location 
  --with-cache            Specify default cache location
  --with-docsdir          Specify documentation location
  --with-webdoc           Install some sample content
  --with-services=svcs    Specify the list of services to install
                           space delimited list of services to install, valid
                           names are:

                           sessionHandler, requestHandler, remote, ae_component, 
                           remote_client, aecgi, httpd, basicauth,
                           oracle, pars, templating, web, fcgiprot, userdir
                           [default: install all services]

  --with-sudo=/path/sudo  Specify path to sudo
  --without-mod_skunkweb  Don't build mod_skunkweb
  --with-apxs=/path/apxs  Specify path to the apxs program
])


AC_OUTPUT(Makefile pylibs/Makefile pylibs/skunkdoc/skunkdoc.py pylibs/skunkdoc/skunkdoc2html.py pylibs/skunkdoc/skunkdoc2latex.py docs/Makefile)
dnl pylibs/mmlib/Setup)

